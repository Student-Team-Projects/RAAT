apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlinx-serialization'
apply plugin: 'kotlin-parcelize'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 33

    defaultConfig {
        applicationId "com.gaurav.avnc"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 14
        versionName "1.9.2"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/roomSchema/".toString()]
            }
        }

        externalNativeBuild {
            cmake {
                version '3.22.1'
                //We are not using STL
                arguments '-DANDROID_STL=none'
                targets 'native-vnc', 'vncclient', 'turbojpeg-static', 'wolfssl'
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {

        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix ' (debug)'
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        ci {
            initWith(release)
            applicationIdSuffix '.ci'
            versionNameSuffix ' (CI)'
        }
    }

    buildFeatures {
        dataBinding true
    }

    externalNativeBuild {
        cmake {
            path file('CMakeLists.txt')
        }
    }

    bundle {
        density {
            enableSplit false
        }
        language {
            enableSplit false
        }
    }
}

dependencies {
    implementation "androidx.core:core-ktx:1.9.0"
    implementation 'androidx.activity:activity-ktx:1.6.1'
    implementation "androidx.fragment:fragment-ktx:1.5.4"
    implementation "androidx.appcompat:appcompat:1.5.1"
    implementation "androidx.dynamicanimation:dynamicanimation:1.0.0"
    implementation "androidx.biometric:biometric-ktx:1.2.0-alpha05"
    implementation "androidx.preference:preference-ktx:1.2.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1"
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    implementation "androidx.constraintlayout:constraintlayout:2.0.4"
    implementation "androidx.drawerlayout:drawerlayout:1.1.1"
    implementation "androidx.room:room-runtime:2.4.3"
    implementation "androidx.room:room-ktx:2.4.3"
    kapt "androidx.room:room-compiler:2.4.3"

    implementation "com.google.android.material:material:1.7.0"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.4.1"
    implementation "org.connectbot:sshlib:2.2.19"

    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test:runner:1.4.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:3.4.0"
    androidTestImplementation "androidx.test.espresso:espresso-intents:3.4.0"
    androidTestImplementation "androidx.test:rules:1.4.0"
    androidTestImplementation "androidx.test.ext:junit-ktx:1.1.3"
    androidTestImplementation "io.mockk:mockk-android:1.12.0"
}